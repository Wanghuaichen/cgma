################################################################################
#                           Standard Stuff
################################################################################
AC_INIT(CGM, 10.0b)
AC_CONFIG_AUX_DIR(./config_aux)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(CGM,10.0b)
AC_DISABLE_SHARED

SNL_CHECK_COMPILERS
AM_CONDITIONAL(build_parallel, [test "x$WITH_MPI" != "xno"])
AC_PROG_LIBTOOL
LIBS="-lm"
AC_PROG_LIBTOOL
AC_C_BIGENDIAN( [LITTLE_ENDIAN=], [LITTLE_ENDIAN=-DLITTLE_ENDIAN] )
AC_SUBST(LITTLE_ENDIAN)



################################################################################
#                              MPI OPTIONS
################################################################################

if test "x$WITH_MPI" != "xno"; then
  CXXFLAGS="$CXXFLAGS -DUSE_MPI"
  CGM_PARALLEL_INCLUDE='-I${CGM_DIR}/geom/parallel'
  CGM_PARALLEL_LIB='-L${CGM_DIR}/geom/parallel -lcubit_parallel'
  CGM_PARALLEL_LIB_FILE='${CGM_LIBDIR}/libcubit_parallel.a'
fi
AM_CONDITIONAL(USE_MPI, [test "xno" != "x$WITH_MPI"])
AC_SUBST(CGM_PARALLEL_INCLUDE)
AC_SUBST(CGM_PARALLEL_LIB)
AC_SUBST(CGM_PARALLEL_LIB_FILE)

################################################################################
#                           CGM-specific Checks
################################################################################

SNL_CANT_USE_STD
SNL_TEMPLATE_DEFS_INCLUDED
AC_SUBST(CANT_USE_STD)
AC_SUBST(CANT_USE_STD_IO)
AC_SUBST(TEMPLATE_DEFS_INCLUDED)
AM_CONDITIONAL(INCLUDE_TEMPLATE_DEFS, test x$TEMPLATE_DEFS_INCLUDED != x)


################################################################################
#                         Use Cubit shared libraries
################################################################################
CGM_LIB_LINK='$(CGM_LIB_LINK_INTERNAL)'
CGM_LIB_FILES='$(CGM_LIB_FILES_INTERNAL)'
CGM_LIB_LTFLAGS=
CUBIT_LINK=no
CUBIT_FILE=no
CUBIT_BIN_DIR=no

AC_ARG_WITH( CUBIT,
             AC_HELP_STRING([--with-CUBIT=<dir>],[Use CGM from CUBIT shared library]),
             [CUBIT_DIR="$withval"],[CUBIT_DIR=no])
if test "x$CUBIT_DIR" == "xyes"; then
  AC_MSG_ERROR([Option --with-CUBIT without specifying Cubit directory.])
elif test "x$CUBIT_DIR" != "xno"; then
  CUBIT_BIN_DIR="$CUBIT_DIR/bin"
  cat >conftest.cc <<EOCUBITCT
class AcisQueryEngine { public: static AcisQueryEngine* instance_; };
int main() {
  AcisQueryEngine::instance_ = 0;
  return 0;
}
EOCUBITCT
  pattern=["${CUBIT_BIN_DIR}/*cubiti[0-9][0-9].*"]
  for file in $pattern; do
    AC_MSG_CHECKING([for AcisQueryEngine::instance_ in $file])
    num=[`expr X"$file" : X".*cubiti\([0-9][0-9]\)\..*"`]
    if ./libtool --mode=link $CXX $LDFLAGS -L$CUBIT_BIN_DIR -R$CUBIT_BIN_DIR -lcubiti$num conftest.cc -o conftest >&5 2>&5; then
      CUBIT_LINK="-L\$(CUBIT_BIN_DIR) -lcubiti$num"
      CUBIT_FILE=`expr x"$file" : x"$CUBIT_BIN_DIR/\(.*\)"`
      CUBIT_FILE="\$(CUBIT_BIN_DIR)/$CUBIT_FILE"
      AC_MSG_RESULT(yes)
    else
      AC_MSG_RESULT(no)
    fi
  done
  rm -f conftest.cc
  
  if test "x$CUBIT_LINK" = "xno"; then
    AC_MSG_ERROR("Cubit library not found in $CUBIT_BIN_DIR")
  fi
  CGM_LIB_LINK='$(CUBIT_LINK)'
  CGM_LIB_FILES='$(CUBIT_FILE)'
  CGM_LIB_LTFLAGS='-R$(CUBIT_BIN_DIR)'
fi

AC_SUBST(CUBIT_FILE)
AC_SUBST(CUBIT_LINK)
AC_SUBST(CUBIT_BIN_DIR)
AC_SUBST(CGM_LIB_LINK)
AC_SUBST(CGM_LIB_FILES)
AC_SUBST(CGM_LIB_LTFLAGS)

AM_CONDITIONAL(BUILD_CGM,[test x"$CUBIT_DIR" == x"no"])


################################################################################
#                           ACIS OPTIONS
################################################################################
ACIS_BASE_LIBS='-lSpaAVis -lSpaAWarp -lSpaASurf -lSpaALops -lSpaABlend -lSpaACIS -lSpaBase'
ACIS_STEP_LIBS='-lacisstep -lxstep'
ACIS_IGES_LIBS='-lacisiges -lxiges'
CGM_ACIS_LIBS='-lcubit_ACIS'

AC_ARG_WITH( ACIS, 
             AC_HELP_STRING([--with-ACIS=<dir>],[Build with ACIS support, specify directory where ACIS is installed.]),
             [ACIS_DIR=$withval],[ACIS_DIR=no] )
AC_ARG_WITH( ACIS-system, AC_HELP_STRING([--with-ACIS-system=SYS], 
          [Specify ACIS system name (e.g. linux_so), default is to autodetect.]),
             [ACIS_SYSTEM=$withval],[ACIS_SYSTEM=] )
AC_ARG_WITH( ACIS-version, AC_HELP_STRING([--with-ACIS-version=INT],
[Specify ACIS version as an integer value (100*major+10*minor+point), default is to autodetect.]),
[if ! test "$withval" -gt "600"; then
  AC_MSG_ERROR("ACIS-version must be an integer greater than 600.")
 fi
 ACIS_VERSION=$withval],
[ACIS_VERSION=0])
if test "x$ACIS_DIR" == "x"; then 
  ACIS_DIR=no; 
fi          
if test "$ACIS_DIR" == "no"; then
  ACIS_LIB_DIR=".";
fi
if test "$ACIS_DIR" != "no"; then
  if test "x$CUBIT_DIR" != "xno"; then
    AC_MSG_ERROR("Conflicting options:  Cannot specifiy both --with-ACIS and --with-CUBIT")
  fi

  if test "$ACIS_DIR" == "yes"; then ACIS_DIR=.; fi
  SNL_ACIS_ENV
  
  SNL_ACIS_TRANSLATOR
  if test x$ACIS_STEP_TRANSLATOR != x; then
    ACIS_LIBS="$ACIS_LIBS $ACIS_STEP_LIBS"
    ACIS_XLIBS="$ACIS_XLATE_LIBS"
  fi
  if test x$ACIS_IGES_TRANSLATOR != x; then
    ACIS_LIBS="$ACIS_LIBS $ACIS_IGES_LIBS"
    ACIS_XLIBS="$ACIS_XLATE_LIBS"
  fi
  ACIS_LIBS="$CGM_ACIS_LIBS $ACIS_LIBS $ACIS_XLIBS $ACIS_BASE_LIBS"
  ACIS_HEALER="-DACIS_HEALER"
  ACIS_INCLUDES="-I$ACIS_DIR/include"
  ACIS_DEFS='$(ACIS_STEP_TRANSLATOR) $(ACIS_IGES_TRANSLATOR) -DCUBIT_ACIS_VERSION=$(ACIS_VERSION) -DACIS_VERSION=$(ACIS_VERSION) -D$(ACIS_PLATFORM) -DACIS_LOCAL_OPS'
fi
AC_SUBST(ACIS_DEFS)
AC_SUBST(ACIS_DIR)
AC_SUBST(ACIS_LIB_DIR)
AC_SUBST(ACIS_INCLUDES)
AC_SUBST(ACIS_LIBS)
AC_SUBST(ACIS_HEALER)
AC_SUBST(ACIS_VERSION)
AC_SUBST(ACIS_PLATFORM)
AC_SUBST(ACIS_STEP_TRANSLATOR)
AC_SUBST(ACIS_IGES_TRANSLATOR)
AM_CONDITIONAL(build_ACIS, test x$ACIS_DIR != xno)



################################################################################
#                           Output Files
################################################################################
AC_MSG_RESULT([CXXFLAGS = $CXXFLAGS])
AC_OUTPUT_COMMANDS( [if test -f util/CubitUtilConfigure.h; then true; else echo "#define CUBIT_UTIL_EXPORT" > util/CubitUtilConfigure.h; fi],
                    [if test -f geom/CubitGeomConfigure.h; then true; else echo "#define CUBIT_GEOM_EXPORT" > geom/CubitGeomConfigure.h; fi] )
AC_CONFIG_FILES(Makefile 
           util/Makefile
           geom/Makefile
           geom/ACIS/Makefile
           geom/Cholla/Makefile
           geom/facet/Makefile
           geom/facetbool/Makefile
           geom/parallel/Makefile
           geom/virtual/Makefile 
           cgm.make)

AC_OUTPUT
